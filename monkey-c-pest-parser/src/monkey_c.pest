EOL        = _{ "\r" | "\n" }
WHITESPACE = _{ " " | "\t" | EOL }

// TODO: We need to keep comments.
COMMENT    = _{
    "//" ~ (!EOL ~ ANY)* |
    "/*" ~ (!"*/" ~ ANY)* ~ "*/"
}

semi       = _{ ";" }
quote      = _{ "\"" }

ident          = @{ (ASCII_ALPHANUMERIC | "_" | ".")+ }
string_literal = { quote ~ ("\\\"" | (!quote ~ ANY))* ~ quote }
basic_literal  = { NUMBER+ | string_literal }

container = { ident ~ "<" ~ ident ~ "?"? ~ ("or" ~ ident ~ "?"?)* ~ ">" }
alias  = { "as" ~ (container | ident) ~ "?"? }
field  = { (basic_literal | ident)  ~ alias? }
import = { "import" ~ ident ~ semi }
using  = { "using" ~ ident  ~ alias? ~ semi }

call_args  = _{ ( field | basic_literal) }
call       = {
    (
        ident ~ "()" |
        ident ~ "(" ~ call_args ~ ("," ~ call_args)* ~ ")"
    )
}

array = { "[" ~ field ~ ("," ~ field)* ~ "]" }

visibility = { "private" | "protected" | "public" }
module     = { "module" ~ ident ~ "{" ~ (class | function | assign | declare)* ~ "}" }
class      = { "class" ~ ident ~ ("extends" ~ ident)? ~ "{" ~ (class | function | assign | declare)* ~ "}" }
function   = { "function" ~ call ~ alias? ~ "{" ~ (class | function | call ~ semi | assign | declare)* ~ "}" }

new     = { "new" ~ call }
declare = { visibility? ~ "var" ~ ident ~ alias? ~ semi }
assign  = { visibility? ~ "var"? ~ ident ~ "=" ~ (new | expr | call | array | basic_literal | ident)+ ~ alias? ~ semi }

integer = @{ ASCII_DIGIT+ }
number  = @{ integer ~ ("." ~ integer?)? }

unary_minus = { "-" }
primary     = _{ number | ident | "(" ~ expr ~ ")" }
atom        = _{ unary_minus? ~ primary }

bin_op   = _{ add | subtract | multiply | divide | modulo }
add      = { "+" }
subtract = { "-" }
multiply = { "*" }
divide   = { "/" }
modulo   = { "%" }

expr = { atom ~ (bin_op ~ atom)* }

document = {
    SOI ~
    (
        import |
        using |
        module |
        class |
        function
    )+
    ~ EOI
}

arithmetic_operator = {
    "+"  |
    "-"  |
    "*"  |
    "/"  |
    "%"  |
    "++" |
    "--"
}

relational_operator = {
    "==" |
    "!=" |
    ">"  |
    "<"  |
    ">=" |
    "<="
}

logical_operator = {
    "&" |
    "|" |
    "!"
}

bitwise_operator = {
    "&" |
    "|" |
    "^" |
    "~"
}

assignment_operator = {
    "="   |
    "+="  |
    "-="  |
    "*="  |
    "/="  |
    "%="  |
    "<<=" |
    ">>=" |
    "&="  |
    "|="  |
    "^="
}

miscellaneous_operator = {
    "?" |
    ":" |
    "new"
}

keyword = {
    "and"        |
    "as"         |
    "break"      |
    "catch"      |
    "case"       |
    "class"      |
    "const"      |
    "continue"   |
    "default"    |
    "do"         |
    "else"       |
    "enum"       |
    "extends"    |
    "false"      |
    "finally"    |
    "for"        |
    "function"   |
    "has"        |
    "hidden"     |
    "if"         |
    "instanceof" |
    "me"         |
    "module"     |
    "nan"        |
    "native"     |
    "new"        |
    "null"       |
    "or"         |
    "private"    |
    "protected"  |
    "public"     |
    "return"     |
    "self"       |
    "static"     |
    "switch"     |
    "throw"      |
    "true"       |
    "try"        |
    "using"      |
    "var"        |
    "while"
}
